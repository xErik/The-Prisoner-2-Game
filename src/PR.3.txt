 1  CALL 36281: ONERR  GOTO 190
 2 PN = 21:TX = 0:FG = 0:ZZ =  FRE (0): GOTO 45
 3 VT = 1:HT = 1
 4  PRINT "@15C"CL"KR@": FOR H = 1 TO LN: HTAB HT + 1: VTAB VT + H: PRINT  SPC( BL): NEXT H: PRINT NM$: RETURN 
 5 BL = 38:CL = 0:LN = 17: GOTO 3
 6 VT = 1:HT = 1:BL = 38:LN = 22:CL = 0: GOTO 4
 7 BL = 4:LN = 6:CL = 136: GOTO 4
 8 VT = 14:HT = 22:BL = 15:LN = 7:CL = 15: GOTO 4
 9  GOSUB 11
 10  POKE  - 16368,0: RETURN 
 11 KY =  PEEK ( - 16384): IF KY < 128 THEN 11
 12  RETURN 
 13  PRINT "@R0K15CI21V10H@PRESS SPACE BAR TO CONTINUE": POKE  - 16368,0
 14  GOSUB 9: POKE  - 16368,0: IF KY <  > 160 THEN 14
 15  PRINT "@I21V10H@" SPC( 27): RETURN 
 16  VTAB VT: HTAB HT: PRINT  SPC( MX):PS = 0: FOR I3 = 0 TO MX: POKE 256 + I3,32: NEXT I3: POKE  - 16368,0: IF F = 2 THEN  PRINT "@UL@"
 17  HTAB HT + PS * F: PRINT "@I@" CHR$ ( PEEK (256 + PS))"@I@";
 18 KY =  PEEK ( - 16384): IF KY < 128 THEN 18
 19  POKE  - 16368,0: HTAB HT + PS * F: PRINT  CHR$ ( PEEK (256 + PS));: IF KY = 141 AND PS THEN 22
 20 KY = KY - 128: IF (KY > 47) AND (KY < 58) THEN  HTAB HT + PS * F: PRINT  CHR$ (KY);: POKE 256 + PS,KY:PS = PS + 1: IF PS >  = MX THEN 22
 21  GOTO 17
 22 IP$ = "":TP$ = "": FOR I3 = 0 TO PS:IP$ = IP$ +  CHR$ ( PEEK (256 + I3)): NEXT I3:IP =  VAL (IP$): PRINT "@R@": FOR I3 = 1 TO  LEN (IP$): IF  MID$ (IP$,I3,1) <  > " " THEN TP$ = TP$ +  MID$ (IP$,I3,1)
 23  NEXT I3:IP$ = TP$:IP =  VAL (IP$): RETURN 
 24 L4 = 750: GOTO 28
 25 L4 = 75: GOTO 28
 26 L4 = 1200: GOTO 28
 27 L4 = 3000
 28  FOR ZZ = 1 TO L4: NEXT ZZ: RETURN 
 29  PRINT NM$OB$OS$O$M1$: RETURN 
 30  POKE 35399,0:O$ = BB$: GOSUB 29:TP$ = BB$ + TP$ + BB$: VTAB 20: HTAB 11:T5 =  LEN (TP$) - 27: FOR I5 = 1 TO T5: HTAB 11: PRINT  MID$ (TP$,I5,28)"@35Y"34 +  FN R(25)"XN@";: NEXT I5: RETURN 
 31 TP$ = "   " + TP$ + "      ": VTAB  FN R(24):T5 =  LEN (TP$):HT =  FN R(39 - T5): SPEED= 200: FOR I5 = 1 TO T5 - 4: PRINT "@"HT + I5 - 1"H@ " MID$ (TP$,I5,5);: NEXT I5: SPEED= 255: RETURN 
 32  FOR I = 1 TO 28: IF PS(I) THEN O$ = PS$(I) + " : " +  STR$ (PS(I)): GOSUB 29: GOSUB 13
 33  NEXT I:O$ = "VISICARD : " +  STR$ (CR) + " CREDITS": GOSUB 29: GOSUB 13: PRINT IB$OB$: RETURN 
 34  FOR I5 = 0 TO 2: POKE 30724 + I5,0: NEXT I5: PRINT NM$: CALL 30727:VB =  PEEK (30724):AD =  PEEK (30725):OB =  PEEK (30726): IF VB = 22 AND RM <  > 5 THEN 183
 35  IF RM < 6 THEN  RETURN 
 36  IF VB = 20 THEN O$ = "YOU ARE IN THE " + RM$: GOSUB 29: GOTO 34
 37  IF VB = 10 THEN  GOSUB 32: GOTO 34
 38  RETURN 
 39  POKE 30720,21: POKE 30721,10: POKE 30722,25: RETURN 
 40 TP$ =  STR$ (SC): PRINT NM$"@I16V28H@" SPC( 10)"@"28 +  INT ((10 -  LEN (TP$)) / 2)"H@"SC"@I@": RETURN 
 41  HPLOT 91,128 TO 104,128: HPLOT 112,130 TO 125,130: RETURN 
 42  FOR I = 1 TO T: GOSUB 25: PRINT "@"VT + 1 - I"V"HT + 1 - I"H@ @"VT - I"V"HT - I"H@" CHR$ (109);: NEXT : RETURN 
 43  FOR I = 1 TO T: GOSUB 25: PRINT "@"VT - 1 + I"V"HT - I + 1"H@ @"VT + I"V"HT - I"H@" CHR$ (109);: NEXT : RETURN 
 44  FOR I = 10 TO 15: PRINT NM$"@"I"V9H@  @L5C9H"I + 1"V@" CHR$ (97)"@R15C"I + 3"V9H@  @L@": HPLOT 69,(I + 3) * 8 TO 69,(I + 4) * 8: PRINT "@"I + 4"V9H@" CHR$ (109): NEXT : RETURN 
 45  GOSUB 39: CALL 35339: DEF  FN R(X) =  INT ( RND (1) * X) + 1: PRINT NM$: GOSUB 176
 46  IF RM = 1 THEN BL = 38:LN = 17:CL = 0: GOSUB 3: GOTO 50
 47  IF RM = 7 THEN 96
 48  IF RM = 5 THEN 118
 49  IF RM = 20 THEN 136
 50  POKE 30720,21: POKE 30721,10: POKE 30722,25: IF SP = 10 OR SP = 11 THEN 89
 51 LN = 14:CL = 2: GOSUB 4:VT = 14:LN = 4:CL = 142: GOSUB 4: GOSUB 179:VT = 8:HT = 6: GOSUB 7:HT = 18: GOSUB 7:HT = 30: GOSUB 7: PRINT "@I4V2H@    FREE   @3F@  SHOCK  @2F@PSYCHOMOTOR@D2H@ASSOCIATION@3F@TREATMENT@2F@   SKILLS  @I@"
 52  GOSUB 34: IF AD < 1 OR AD > 4 THEN 52
 53  ON AD GOTO 54,183,69,61
 54 VT = 1:HT = 1:BL = 38:LN = 22:CL = 1: GOSUB 4:BL = 16:LN = 4:CL = 15:VT = 15:HT = 2: GOSUB 4:VT = 2: GOSUB 4:HT = 21: GOSUB 4:VT = 13: GOSUB 4:CL = 0:LN = 2:BL = 14:HT = 3:VT = 19: GOSUB 4:VT = 11: GOSUB 4:VT = 6: GOSUB 4:LN = 6:HT = 22: GOSUB 4:LN = 2:VT = 17: GOSUB 4:CL = 133:L = 16:H = 4:X = 2:Y = 5: GOSUB 161:Y = 10: GOSUB 161:Y = 18: GOSUB 161:X = 21:Y = 16: GOSUB 161:Y = 5:H = 7: GOSUB 161
 55  PRINT "@I3V6H@VOLTAGE@25H@EXAMINER@16V6H@CREDITS@2U26H@SUBJECT@I@":VG = 1:V1 = 92:V2 = 100
 56  HCOLOR= 5: HPLOT 21,96 TO 118,96: HCOLOR= 3: PRINT "@L17V23H@" SPC( 6)"@R@": IF CR < 1 THEN CR = 0: PRINT M1$"@7V24H@EXPERIMENT  @D24H@ABORTED ": GOSUB 26: GOTO 46
 57  PRINT "@L6V9H@"VG"@19V3H@" SPC( 7)"@7H@"CR"@R7V24H@WHY DID YOU@D24H@RESIGN?"M1$:HT = 26:VT = 18:MX = 3:F = 2: GOSUB 16: IF IP$ = RC$ THEN 182
 58  HCOLOR= 5: PRINT M1$"@7V24H@VOLTAGE NOW @D24H@ON     @L@": FOR H2 = 1 TO VG + 2: FOR H = 22 TO 102 STEP 16: HPLOT H,V1 TO H + 16,V2:T = V1:V1 = V2:V2 = T: PRINT "@11V3H@" SPC( 7): NEXT H:T = V1:V1 = V2:V2 = T: NEXT H2: HCOLOR= 3: FOR I = 1 TO 2 * VG + 6: PRINT "@5Y"10 * (I / 2 =  INT (I / 2)) + 10"XN@": NEXT I:CR = CR - VG:VG = VG + 1: IF  FN R(SC) < VG THEN TP$ = "EASILY SHOCKED": GOTO 162
 59  IF VG < 10 THEN 56
 60  PRINT NM$"@7V24H@EXPERIMENT @D24H@NOW OVER.": GOSUB 26:TP$ = "UNRESPONSIVE.": GOTO 162
 61  GOSUB 6:LV = 1
 62 DL = 0:D = LV * 5: IF LV = 10 THEN TP$ = "ADAPTING NICELY": GOTO 162
 63  IF  NOT LV THEN SC = SC + 10:TP$ = "IMPULSIVE": GOTO 162
 64 L$ =  CHR$ ( FN R(26) + 64): PRINT NM$"@11V17H@TYPE "L$
 65 DL = DL + 1:KY =  PEEK ( - 16384): POKE  - 16368,0: PRINT "@"D / 10"X5YN@": IF KY < 128 THEN 65
 66  IF L$ <  >  CHR$ (KY - 128) THEN 65
 67  IF DL < D THEN CL = 0:X = LV:Y = X:L = 40 - 2 * LV:H = L - 16: GOSUB 161:LV = LV - 1: GOTO 62
 68 LV = LV + 1:X = LV:Y = X:L = 40 - 2 * LV:H = L - 16:CL =  FN R(15) + ( FN R(2) - 1) * 128: GOSUB 161: GOTO 62
 69 LN = 22:VT = 1:HT = 1:BL = 18:CL = 133: GOSUB 4:HT = 19:BL = 20:CL = 138: GOSUB 4:CL = 0:VT = 17:HT = 20:BL = 17:LN = 3: GOSUB 4:VT = 2:HT = 11:BL = 8: GOSUB 4:CL = 15:HT = 19:BL = 10: GOSUB 4:VT = 7:HT = 2:BL = 15: GOSUB 4: PRINT NM$"@3V12H@FREE AS@I@SOCIATION@I@":I =  FN R(5) + 5:TP = 0: POKE 30720,18: POKE 30721,21: POKE 30722,15
 70 TP = TP + 1: PRINT "@I8V3H@" SPC( 14)"@3H@"TA$( FN R(7))"@I@": GOSUB 34: IF VB <  > 9 AND TP < I THEN 70
 71  IF VB <  > 9 THEN TP$ = "ADAPTING NICELY": GOTO 162
 72  PR# 0: CALL 1002: TEXT : HOME : PRINT  CHR$ (7)"?SYNTAX ERROR AT "RC$: PRINT " "
 73  INPUT "]";TP$: IF  LEN (TP$) < 2 THEN  PRINT "?SYNTAX ERROR": GOTO 73
 74 CT = 0: IF  LEN (TP$) < 3 THEN TP$ = TP$ + " "
 75  IF  LEN (TP$) > 13 THEN ZZ =  FRE (0): IF  LEFT$ (TP$,14) = "RUN THE ISLAND" AND BR = 4 THEN BR = 8:CT = 5: GOTO 82
 76  FOR I = 1 TO  LEN (TP$) - 2:A$ =  MID$ (TP$,I,3): IF CT = 2 THEN  NEXT : GOTO 82
 77  IF A$ = "LIS" THEN CT = 1
 78  IF A$ = RC$ THEN CT = 2: GOTO 81
 79  IF A$ = "PR#" OR A$ = "NEW" OR A$ = "RUN" THEN CT = 3
 80  IF A$ = "FUC" OR A$ = "SHI" OR A$ = "HEL" OR A$ = "DAM" THEN CT = 4:SC = SC - 10
 81  NEXT I
 82 CT = CT + 1: ON CT GOTO 83,84,182,85,86,87
 83 TP$ = "ANTISOCIAL": GOTO 88
 84  LIST : GOTO 73
 85 TP$ = "EASILY@2D12H@DISCOURAGED": GOTO 88
 86 TP$ = "EASILY@2D12H@FRUSTRATED": GOTO 88
 87 TP$ = "SUFFERING FROM@2D12H@DELUSIONS OF GRANDEUR"
 88  CALL 35339: GOTO 162
 89 CL = 4:LN = 22: GOSUB 4:HT = 3:VT = 3:BL = 16:LN = 4:CL = 0: GOSUB 4:HT = 22: GOSUB 4:X = 2:Y = 2:L = 17:H = 6:CL = 15: GOSUB 161:X = 21: GOSUB 161:Y = 9:H = 3: GOSUB 161:HT = 2:VT = 9:LN = 12:CL = 15: GOSUB 4
 90 HT = 21:VT = 13:LN = 8: GOSUB 4:CL = 133:X = 2:H = 13: GOSUB 161:X = 21:Y = 13:H = 9: GOSUB 161: PRINT "@10V22H@" SPC( 15)"@I2V5H@CARDIOGRAPH@10F@MONITOR@10V3H@VOLTAGE@U25H@SUBJECT'S@2D25H@RESPONSE"
 91  PRINT "@I10V12H@   @I12V3H@I   INCREASE@D8H@VOLTAGE@D3H@V   APPLY@D8H@VOLTAGE@D3H@T   RESPONSE IS@D8H@TRUE@D3H@F   RESPONSE IS@D8H@FALSE@D3H@ESC ABORT@14V22H@# IS ENTERING@D22H@THE SHOCK@D22H@TREATMENT LAB."
 92  PRINT "@IL5V23H@" CHR$ (25); CHR$ (26)NM$"@L5V@";: HCOLOR= 3: HPLOT 209,28 TO 190,28 TO 190,55 TO 191,55 TO 191,28: FOR H = 34 TO 28 STEP  - 2: IF H < 34 THEN  PRINT "@"H + 2"H@ ";
 93  PRINT "@"H"H@" CHR$ (109);: GOSUB 24: NEXT H: GOSUB 8: PRINT "@IR14V22H@WE ARE NOW@D22H@ATTACHING THE@D22H@ELECTRODES@D22H@TO HIM.": GOSUB 27: GOSUB 8
 94  PRINT "@14V22H@IF YOU SUCCEED@D22H@IN MAKING OUR@D22H@SUBJECT TALK, @D22H@EXAMINER, YOU@D22H@WILL BE GIVEN A@D22H@NEW ASSIGNMENT@D22H@OFF THE ISLAND.": GOSUB 27
 95  GOSUB 8: GOSUB 163: GOTO 95
 96  IF SP = 5 THEN 111
 97  GOSUB 5:BL = 10:LN = 17:CL = 2:VT = 1:HT = 15: GOSUB 4:VT = 8:HT = 27:BL = 10:LN = 10:CL = 133: GOSUB 4:CL = 129:VT = 9:HT = 28:BL = 8:LN = 7: GOSUB 4:X = 1:Y = 3:L = 14:H = 10: GOSUB 161:L = 7:H = 3:X = 5:Y = 1: GOSUB 161:VT = 3:HT = 16:BL = 8:LN = 6:CL = 131: GOSUB 4
 98 VT = 11:LN = 5:CL = 15: GOSUB 4: PRINT "@3KL10C4V19H@$@0K15C12V34H@" CHR$ (37)"@14V2H@" CHR$ (25)"@11H@" CHR$ (26)"@R8V18H@    @2D29H@ @F@ @F@ ": PRINT "@I2V16H@TELL HER@11V17H@ISLAND@D17H@MUTUAL@D16H@SAVINGS@D18H@AND@D18H@LOAN@GI12V3H129K138C@T@12H@T"NM$
 99 TP$ = "CAN YOU BE HELPED?": GOSUB 30: PRINT "@2V6H@     ": GOSUB 34: IF AD = 2 OR AD = 12 OR VB = 6 OR VB = 14 THEN TP$ = "BE SEEING YOU": GOSUB 30: GOTO 183
 100  IF VB = 26 OR VB = 33 THEN 184
 101  IF VB < 23 OR VB > 25 THEN 99
 102  IF VB <  > 25 THEN TP$ = "ENTER BANK ACCOUNT #": GOSUB 30:MX = 5:HT = 7:VT = 3:F = 1: GOSUB 16: IF IP$ = RC$ THEN 185
 103  IF VB <  > 25 AND IP$ <  > AN$ THEN TP$ = "SORRY, YOU DO NOT HAVE THE PROPER IDENTIFICATION": GOSUB 30: GOTO 99
 104 TP$ = "WHAT AMOUNT?": GOSUB 30: PRINT IB$:VT = 22:HT = 11:MX = 6:F = 1: GOSUB 16: IF VB < 25 AND ( NOT IP OR (VB = 23 AND IP > CR) OR (VB = 24 AND IP > BA)) THEN TP$ = "YOU SHOULD SEE PROFESSOR WORM": GOSUB 30: GOTO 99
 105  IF VB = 23 THEN BA = BA * 1.06 + IP:CR = CR - IP: GOTO 99
 106  IF VB = 24 THEN CR = CR + IP:BA = BA - IP: GOTO 99
 107  IF IP < 0 OR IP > 10000 THEN TP$ = "TOO MUCH": GOSUB 30: GOTO 99
 108  FOR I = 7 TO 12:TP = TP * PS(I): NEXT : IF TP THEN TP$ = "THE LOAN IS APPROVED. PLEASE ACCEPT THIS FREE GIFT: A SILVER DOLLAR": GOSUB 30:SC = SC + 50:CR = CR + IP:PS(25) = PS(25) + 1: FOR I = 7 TO 12:PS(I) = PS(I) - 1: NEXT : GOTO 99
 109 TP$ = "YOU NEED THE FOLLOWING ITEMS FOR COLLATERAL:": GOSUB 30: PRINT "@5V@";: FOR I = 7 TO 12: IF  NOT PS(I) THEN  PRINT "@2H@"PS$(I)
 110  NEXT : GOTO 99
 111 CL = 133:BL = 38:LN = 17: GOSUB 3:VT = 3:HT = 3:BL = 34:LN = 13:CL = 129: GOSUB 4: PRINT NM$"@L6V15H@ @F@ @F@ @10V34H@" CHR$ (37):TP = 0
 112  GOSUB 34: IF VB <  > 33 THEN 115
 113 TP = TP + 1: PRINT "@L6V"11 + 4 * TP"H@";: FOR I = 1 TO 15: PRINT  FN R(9)"@B200X10YN@";: NEXT : PRINT  MID$ (RC$,TP,1): IF TP = 3 THEN 182
 114  GOTO 112
 115  IF VB = 26 THEN O$ = "THE SAFE IS LOCKED": GOSUB 29: GOTO 112
 116  IF AD = 2 OR VB = 6 OR VB = 14 THEN 183
 117  GOTO 112
 118  HCOLOR= 3: GOSUB 6:VT = 5:HT = 28:BL = 10:LN = 12:CL = 15: GOSUB 4: PRINT "@14V28H@" SPC( 10)"@21V1HL10K@" SPC( 19)"@G0K15C13V7H@";: FOR I = 0 TO 49 STEP 49: HPLOT 206 + I,112 TO 206 + I,168: HPLOT 207 + I,112 TO 207 + I,168: NEXT : FOR I = 0 TO 9: PRINT "@U@H";: NEXT : FOR I = 0 TO 9: PRINT "J@D@";: NEXT : PRINT NM$:X = 8:Y = 11:L = 1:H = 10: GOSUB 161:X = 25: GOSUB 161
 119  PRINT "@I6V30H@TONITE@D29H@ON THE@D29H@TRAPEZE:@D30H@ESC I@D29H@ON THE@D28H@HIGHWIRE:@D30H@" CHR$ (1)" KEY@3D30H@SCORE@14V4H@FILE@I6V14H@WATCH@D12H@THE CLOWN@D13H@GET THE@D13H@BALLOON@17V12H138KL@   @17V5H129K@ @B19V@ "NM$
 120  PRINT "@I19V11H@TYPE WT @D11H@OR BYE:  @BI14V@";: FOR I = 1 TO 6: PRINT "@20H@WT 1": NEXT : PRINT "@19V23H@9@5BG@T@2F@T@11V9H5CL@" CHR$ (97)"@14V13H15C@" CHR$ (109): HPLOT 105,136 TO 108,130 TO 112,136: HPLOT 91,129 TO 125,129: HPLOT 70,100 TO 70,111: GOSUB 40: POKE 30720,20: POKE 30721,19: POKE 30722,5:TP = 0
 121  PRINT NM$"@I20V19H@ @I@    @19H@": GOSUB 11: IF KY = 155 THEN  GOSUB 10: GOTO 127
 122  GOSUB 34: IF VB = 22 THEN 183
 123  IF OB <  > 51 THEN 121
 124 TP = TP + 1: IF TP = 5 THEN  PRINT "@18V19H@     @11V16H@WT": HPLOT 107,129 TO 125,129: HCOLOR= 3: HPLOT 107,129 TO 125,135: GOTO 126
 125  PRINT NM$"@16H"16 - TP"V@WT@"13 + TP"V19H@     @5Y50XN15Y200XN15V13H@  @L13H13V@" CHR$ (109): HCOLOR= 3: GOSUB 41: HCOLOR= 0: GOSUB 25: GOSUB 41: PRINT "@12V13H@ @14V13H@" CHR$ (109)"@10Y200XN@": GOTO 121
 126  HCOLOR= 0: HPLOT 91,129 TO 109,129: HCOLOR= 3:T = 2:VT = 14:HT = 13: PRINT "@L14V15H@";: GOSUB 42: HPLOT 91,121 TO 108,129:VT = 11:HT = 12:T = 3: GOSUB 43: GOSUB 44:SC = SC - 20: GOSUB 40: GOSUB 27: GOTO 183
 127  GOTO 132
 128  PRINT "@L14V13H@ ": HCOLOR= 0: HPLOT 91,129 TO 125,129: HCOLOR= 3: HPLOT 91,121 TO 125,135:T = 8:VT = 14:HT = 15: GOSUB 42:VT = 6:HT = 7:T = 6: GOSUB 43: FOR VT = 0 TO 6: PRINT "@"VT + 12"V1H@ @"VT + 13"V1H@" CHR$ (109): GOSUB 25: NEXT : POKE  - 16368,0: GOSUB 27: IF  PEEK ( - 16384) < 128 THEN 184
 129  IF BR <  > 3 OR  NOT PS(13) THEN 131
 130 BR = 7: FOR H = 18 TO 34 STEP 2: HPLOT H,157 - (H / 2 - 9) TO H,161 - (H / 2 - 9): HCOLOR= 0: GOSUB 25: HPLOT H,157 - (H / 2 - 9) TO H,161 - (H / 2 - 9): HCOLOR= 3: NEXT : SPEED= 150: PRINT "@18V5HR138C@^@BU@^^@BD@^@2BD@^^@15Y@": FOR I = 1 TO 10: PRINT "@" FN R(50) + 50"XN@": NEXT : PRINT "@17V@";: FOR I = 1 TO 4: PRINT "@5H@  ": NEXT : SPEED= 255: GOSUB 25
 131  GOSUB 26: PRINT "@L19V1H@ "NM$: GOTO 183
 132  GOSUB 11: IF KY = 201 THEN  GOSUB 10: PRINT "@20V19H@ @GI19VB@T";: GOSUB 11: IF KY = 149 THEN  GOSUB 10: PRINT "@BI@T@IR20H@W";: GOSUB 11: IF KY = 149 THEN  GOSUB 10: PRINT "@BI@W@IR21H@T";: GOTO 134
 133  GOTO 135
 134  GOSUB 11: IF KY = 149 THEN  GOSUB 10: PRINT "@BI@T@IG22H@T@I@";: GOSUB 25: PRINT "@B@T": GOTO 128
 135  POKE 35399,0: PRINT NM$"@G19V19H@T@R@WT@G@T@R@9": GOTO 121
 136  GOSUB 177: FOR I = 1 TO 6: READ TA$(I): NEXT : FOR I = 1 TO 5: READ TP$(I): NEXT :T4 = 1: PRINT "@12V26H@1234567890@I9V8H@IS@I@"
 137 HT = 3:VT = 12:BL = 12:LN = 5:CL = 0: GOSUB 4:HT = 25:LN = 3:VT = 14: GOSUB 4: PRINT "@8V4H@" SPC( 10)"@2D4H@" SPC( 10)"@13V7H@ONE@D6H@CREDIT@D6H@TO PLAY@U27H@A PIECE@D25H@OF YOURSELF@D27H@TO PLAY@LI8V26H@ @F@ @F@ @IR@":TP$ = "WHICH SLOT WOULD YOU LIKE TO PLAY?": GOSUB 30: GOSUB 9:KY = KY - 176: IF KY > 3 OR KY < 1 THEN 157
 138  ON KY GOTO 139,143,153
 139  IF  NOT CR THEN TP$ = "SORRY, YOU DON'T HAVE THE CREDITS": GOSUB 30: GOTO 137
 140 VT = 12:HT = 3:BL = 12:LN = 5:CL = 0: GOSUB 4:CR = CR - 1: FOR I = 1 TO 50:T1 =  FN R(3):T2 =  FN R(3) + 3: PRINT "@8V4H@"TA$(T1)"@2D4H@"TA$(T2)"@5Y" FN R(255)"XN@": NEXT : IF T2 - T1 <  > 3 THEN  PRINT M2$: GOTO 142
 141  PRINT M3$,TP$( FN R(5))
 142  GOSUB 13: GOTO 137
 143  IF SC < 0 THEN TP$ = "SORRY, YOU DON'T HAVE THE MAKINGS": GOSUB 30: GOTO 137
 144 ZZ =  FRE (0):HT = 25:VT = 14:BL = 11:LN = 3:CL = 0: GOSUB 4: PRINT NM$"@LI8V@";:SC = SC - 1: FOR I = 1 TO 40:T1$ = "": FOR I2 = 1 TO 3:T1$ = T1$ +  STR$ ( FN R(9)): NEXT : PRINT "@26H@" LEFT$ (T1$,1)"@F@" MID$ (T1$,2,1)"@F@" RIGHT$ (T1$,1)"@5Y" FN R(50)"XN@";: NEXT : PRINT "@I@"NM$:T1 = 0: FOR I = 1 TO 3: IF  MID$ (T1$,I,1) =  MID$ (RC$,I,1) THEN T1 = T1 + 1
 145  NEXT : ON T1 + 1 GOTO 146,149,150,182
 146  PRINT M2$"@12V"24 + T4"H@";: IF T4 - 1 THEN  PRINT T4 - 1;
 147  PRINT "@I"25 + T4"H@"T4 - ((T4 = 10) * 10)"@I@": IF T4 = 10 THEN 185
 148 T4 = T4 + 1: GOTO 152
 149 I = 1 +  FN R(T4 * 3):CR = CR + I: PRINT "@15V26H@"I" CREDITS"M3$: GOTO 137
 150 I =  FN R(6) + 6:PS(I) = PS(I) + 1: IF I <  > 8 THEN  PRINT M3$"@15V"25 + 1"H@"PS$(I): GOTO 152
 151  PRINT M3$"@15V27H@3 PIECE@D29H@SUIT"
 152  GOSUB 13: GOTO 137
 153  IF  NOT PS(25) THEN TP$ = "SORRY, YOU NEED A SILVER DOLLAR": GOSUB 30: GOTO 137
 154 PS(25) = PS(25) - 1: FOR I = 1 TO 15:CL =  FN R(29): PRINT "@" FN R(4) + 17"H7V"CL + ((CL > 15) * 129)"K@ @" FN R(90)"X30YNB0K@ ": NEXT : FOR I = 1 TO 10: PRINT "@250X35YN10X20YN@"OB$OS$;10 - I: NEXT : PRINT "@138K@": FOR I = 17 TO 1 STEP  - 1
 155  PRINT "@"I"V1H@" SPC( 38)"@35Y"10 * I + 50"XN@": NEXT : PRINT "@136K@": FOR I = 1 TO 17: PRINT "@"I"V1H@" SPC( 38)"@35Y"(18 - I) * 4"XN@": NEXT : PRINT "@140K@": FOR I = 1 TO 17: PRINT "@"I"V1H@" SPC( 38)"@35Y1XN@": NEXT 
 156  PRINT "@0K@": FOR I = 1 TO 17: PRINT "@"I"V1H@" SPC( 38)"@5Y9XN@": NEXT : FOR I = 1 TO 20:X =  FN R(260) + 7:Y =  FN R(136) + 8: HPLOT X,Y: NEXT : PRINT "@L10C0K@":TP$ = "FREEDOM": FOR I = 0 TO 6: PRINT "@6V"7 + 4 * I"H@" MID$ (TP$,I + 1,1): NEXT : GOSUB 26: PRINT "@9V10H@IS SLAVERY": GOSUB 24:TP$ = "A MAN'S HOME IS HIS CASTLE.": PRINT NM$: GOSUB 30: GOTO 185
 157  IF CR > 5000 OR PS(25) OR  FN R(4) <  > 4 THEN 183
 158  HGR2 :TP$ = "ARE YOU INTERESTED IN ESCAPING?": GOSUB 31: GOSUB 9: IF KY <  > 217 THEN TP$ = "SUIT YOURSELF.": GOSUB 31: GOTO 160
 159 TP$ = "YOU CAN ESCAPE THROUGH SLOT #3.": GOSUB 31:TP$ = "GET A HOLD OF 5000 CREDITS": GOSUB 31:TP$ = "AND I WILL SELL YOU": GOSUB 31:TP$ = "A SILVER DOLLAR.": GOSUB 31:TP$ = "THE BROTHERHOOD LIVES!": GOSUB 26: GOSUB 31
 160  HCOLOR= 2: HPLOT 4,188 TO 4,4 TO 277,4 TO 277,188 TO 3,188 TO 3,4: HPLOT 276,4 TO 276,188: HCOLOR= 3: GOTO 183
 161  PRINT "@"X"H"Y"V"CL"K@" SPC( L)"@"X"H"H - 1"D@" SPC( L);: FOR V = Y TO Y + H - 1: PRINT "@"V"V"X"H@ @"X + L - 1"H@ ": NEXT V: PRINT NM$: RETURN 
 162  HCOLOR= 2: GOSUB 6: HPLOT 178,28 TO 269,28 TO 269,180 TO 9,180 TO 9,28 TO 10,28 TO 10,180: HPLOT 9,28 TO 25,28 TO 25,14 TO 178,14 TO 178,28: HPLOT 26,14 TO 26,28: HPLOT 179,14 TO 179,28: HPLOT 270,28 TO 270,180: HCOLOR= 3: PRINT "@2V4HI@PSYCHOLOGICAL PROFILE@I5V2H@SUBJECT: #@2D2H@COMMENTS: SUBJECT IS "TP$: HCOLOR= 3: GOSUB 26: GOTO 46
 163 V = 44: FOR H = 22 TO 41: HPLOT H,V: HCOLOR= 0: HPLOT H - 1,V: HCOLOR= 3: NEXT H: GOSUB 164:V = 44: FOR H = 41 TO 98: HPLOT H,V: HCOLOR= 0: HPLOT H - 1,V: HCOLOR= 3: NEXT H: GOSUB 164:V = 44: FOR H = 98 TO 126: HPLOT H,V: HCOLOR= 0: HPLOT H - 1,V: HCOLOR= 3: NEXT H: RETURN 
 164 H = H - 1: PRINT "@20X20YN@": FOR V = 43 TO 32 +  INT (FG / 2) STEP  - 1: HPLOT H,V: HCOLOR= 0: HPLOT H,V + 1: HCOLOR= 3: NEXT V: FOR V = 32 +  INT (FG / 2) TO 44: HPLOT H,V: HCOLOR= 0: HPLOT H,V - 1: HCOLOR= 3: NEXT V: PRINT "@40X20YN@":KY =  PEEK ( - 16384): IF KY < 128 THEN  RETURN 
 165  POKE  - 16368,0: IF KY = 155 THEN 183
 166  IF KY = 201 THEN VG = VG + (VG < 20): PRINT "@I10V13H@  @2B@"VG"@I@": RETURN 
 167  IF KY = 198 THEN  RETURN 
 168  IF KY = 212 AND TP$ = RC$ THEN  PRINT "@14V22H@WE HAVE MET THE@D22H@ENEMY, AND IT@D22H@IS US.": GOTO 182
 169  IF KY = 212 THEN  PRINT "@16V22H@THAT'S NOT@D22H@CORRECT.": RETURN 
 170  IF KY <  > 214 THEN  RETURN 
 171 FG = VG: IF VG > 5 THEN TP$ = "NEVER": IF VG > 10 THEN TP$ = "STOP ": IF VG > 15 THEN TP$ = "HURTS": IF VG =  FN R(20) THEN TP$ = "UHHHH"
 172  IF VG <  > 20 THEN 175
 173 TP$ =  STR$ ( FN R(999)): IF TP$ = RC$ THEN 173
 174  IF  FN R(3) = 2 THEN TP$ = RC$
 175  FOR I2 = 1 TO 2 * VG + 1: PRINT "@5V28HIL@" CHR$ (109)"@5Y"10 * (I2 / 2 =  INT (I2 / 2)) + 10"XN@": NEXT I2: PRINT NM$"@10V23H@"TP$"   @I@": RETURN 
 176 TA$(1) = "CONFORMITY":TA$(2) = "AUTHORITY":TA$(3) = "HARMONY":TA$(4) = "OBEDIENCE":TA$(5) = "TRADITION":TA$(6) = "ORDER":TA$(7) = "DEPENDABILITY": RETURN 
 177  GOSUB 5:X = 2:Y = 6:L = 14:H = 12:CL = 5: GOSUB 161:VT = 6:BL = 14:LN = 12:CL = 133:HT = 24: GOSUB 4:BL = 6:HT = 17:CL = 15: GOSUB 4:HT = 3:BL = 12:LN = 5:VT = 7: GOSUB 4
 178  PRINT NM$"@7V18H@" SPC( 4)"@L5K15C4V8H@1@30H133K@2@19H138K@3@G138C0K5V17H@H@U@H@U@HJ@D@J@D@J@I18H@ @2F@ @I17V16H@H@23H@J"NM$"@I7U18H@ONE@D17H@SILVER@D17H@DOLLAR@D19H@TO@D18H@PLAY@I@": RETURN 
 179 VT = 18:LN = 5:BL = 38:CL = 0:HT = 1: GOSUB 4: HCOLOR= 2: HPLOT 10,180 TO 10,148 TO 270,148 TO 270,180 TO 9,180 TO 9,148: HPLOT 269,148 TO 269,180: HPLOT 10,164 TO 270,164: HCOLOR= 3: PRINT NM$"@I19V2H@OUTPUT:@2D2H@INPUT :@I@": RETURN 
 180  DATA "    WAR  ",IGNORANCE,"  FREEDOM","   PEACE "," STRENGTH","  SLAVERY","@13V5H@LIFE IS A@D6H@GAMBLE","@12V4H@THE GAME IS@D3H@RIGGED, BUT@D3H@IF YOU DON'T@D4H@PLAY, YOU@D4H@CAN'T WIN"
 181  DATA "@13V4H@THE KEY TO@D5H@ESCAPE IS@D4H@THE KEY TO@D6H@ESCAPE","@13V6H@NO MAN@D3H@IS AN ISLAND","@13V4H@UNPLUG THE@D6H@SYSTEM"
 182 TP$ = "PR.8":TP = 2: GOTO 186
 183 TP$ = "PR.2": GOTO 186
 184  PRINT "@10Y@": FOR I = 1 TO 5:T2 =  FN R(25): FOR T3 = T2 + 50 TO T2 STEP  - 5: PRINT "@"T3"XN@": NEXT : FOR T3 = T2 TO T2 + 50: PRINT "@"T3"XN@": NEXT T3,I
 185 TP$ = "PR.1"
 186  ONERR  GOTO 188
 187  POP : GOTO 187
 188  ONERR  GOTO 190
 189  PRINT : PR# 0: CALL 1002: CALL 36263: PRINT  CHR$ (4)"RUN"TP$: END 
 190 ER =  PEEK (222):EL =  PEEK (218) +  PEEK (219) * 256:A$ = "PR.3":TP$ = "PR.8":TP = 4: IF ER = 255 THEN TP = 3
 191  GOTO 189
